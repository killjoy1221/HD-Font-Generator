plugins {
    id 'application'
    id "org.ajoberstar.reckon" version "0.16.1"
    id 'org.beryx.jlink' version '2.25.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    // Define the main class for the application.
    mainModule = 'hdfontgen'
    mainClass = 'mnm.hdfontgen.FontGenerator'
}

// append the java version to the dist archives
[distTar, distZip]*.configure{
    archiveClassifier = "java-${java.toolchain.languageVersion.get()}"
}

reckon {
    stages('milestone', 'rc', 'final')

    scopeCalc = calcScopeFromProp() | calcScopeFromCommitMessages()
    stageCalc = calcStageFromProp()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'hdfontgen'
        noConsole = true
    }

    jpackage {
        skipInstaller = true
    }
}

task jpackageZip(type: Zip, dependsOn: ['jpackage']) {
    destinationDirectory = new File(buildDir, "distributions")
    archiveBaseName = "hdfontgen"

    doFirst {
        // read in the jpackage.xml file to get the platform name
        def jpkgFile = new File(jlink.jpackageData.get().imageOutputDir, "${jlink.jpackageData.get().imageName}/lib/app/.jpackage.xml")
        def jpkg = new XmlParser().parse(jpkgFile)
        archiveClassifier = jpkg.@platform
    }

    from jlink.jpackageData.get().imageOutputDir
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
}
