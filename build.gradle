plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id "org.ajoberstar.reckon" version "0.16.1"
    id 'org.beryx.jlink' version '2.25.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javafx {
    version = "17.0.1"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

application {
    // Define the main class for the application.
    mainModule = 'hdfontgen'
    mainClass = 'mnm.hdfontgen.Main'
}

distZip.enabled = false
distTar.enabled = false

reckon {
    stages('build', 'final')

    defaultInferredScope = "patch"
    scopeCalc = calcScopeFromProp() | calcScopeFromCommitMessages()
    stageCalc = calcStageFromProp()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'hdfontgen'
        noConsole = true
    }

    jpackage {
        appVersion = reckon.version.get().normal.toString()
        skipInstaller = true
    }
}
def platform = System.properties["os.name"].split(" ")[0].toLowerCase()
def arch = System.properties["os.arch"]

[Zip, Tar].each { type ->
    task("jpackage${type.simpleName}", type: type, dependsOn: "jpackage") {
        destinationDirectory = new File(buildDir, "distributions")
        archiveClassifier = "$platform-$arch"
        JavaVersion.VERSION_17.toString()

        from jlink.jpackageData.get().imageOutputDir

        if (type === Tar) {
            compression = "GZIP"
        }
    }
}

task jpackageDist {
    dependsOn platform == "linux" ? jpackageTar : jpackageZip
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
}
