plugins {
    id 'application'
    id "org.ajoberstar.reckon" version "0.16.1"
    id 'org.beryx.jlink' version '2.25.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    // Define the main class for the application.
    mainModule = 'hdfontgen'
    mainClass = 'mnm.hdfontgen.FontGenerator'
}

// append the java version to the dist archives
[distTar, distZip]*.configure{
    archiveClassifier = "java-${java.toolchain.languageVersion.get()}"
}
distTar.compression = "GZIP"

reckon {
    stages('build', 'final')

    scopeCalc = calcScopeFromProp() | calcScopeFromCommitMessages()
    stageCalc = calcStageFromProp()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'hdfontgen'
        noConsole = true
    }

    jpackage {
        appVersion = reckon.version.get().normal.toString()
        skipInstaller = true
    }
}
def platform = System.properties["os.name"].split(" ")[0].toLowerCase()
[Zip, Tar].each{type ->
    task("jpackage${type.simpleName}", type: type, dependsOn: "jpackage") {
        destinationDirectory = new File(buildDir, "distributions")
        archiveClassifier = platform

        from jlink.jpackageData.get().imageOutputDir

        if (type === Tar) {
            compression = "GZIP"
        }
    }
}


task jpackageDist {
    dependsOn platform == "linux" ? jpackageTar : jpackageZip
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
}
